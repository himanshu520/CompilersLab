(* This file contains the ML-Yacc specifications of parser for the Tiger Language *)

(* This is the preamble where we can have arbitrary sml code. For us
it is empty *)

%%

(* Name of the parser *)
%name Tiger

(* Produces verbose description of the generated LALR parser *)
%verbose

(* type to capture the position of a token within a file *)
%pos int

(* The terminals or tokens of the language.
   Similar to ML datatype declaration.
   The terminals with no datatype have no value associated with them *)
%term INTEGER of int
    | ID of string
    | TYID of string
    | STRING of string
    | ARRAY
    | BREAK
    | DO
    | ELSE
    | END
    | FOR
    | FUNCTION
    | IF
    | IN
    | LET
    | NIL
    | OF
    | THEN
    | TO
    | TYPE
    | VAR
    | WHILE
    | NOTEQUAL
    | LESSEQUAL
    | GREATEREQUAL
    | ASSIGN
    | LPAREN
    | RPAREN
    | LBRACES
    | RBRACES
    | LBRACKETS
    | RBRACKETS
    | COLON
    | DOT
    | SEMICOLON
    | COMMA
    | PLUS
    | MINUS
    | EQUALS
    | MULTIPLY
    | DIVIDE 
    | LESS
    | GREATER
    | AND
    | OR
    | EOF
    
(* The nonterminals or symbols of the language.
   The types for the nonterminals are the correspoding abstract syntax used to capture the semantic 
   meaning of the values associated with the nonterminal *)
%nonterm argList            of Absyn.Exp list
       | argListTail        of Absyn.Exp list
       | arrCreate          of Absyn.Exp
       | assignment         of Absyn.Exp
       | callExp            of Absyn.Exp
       | dec                of Absyn.Dec
       | decList            of Absyn.Dec list
       | exp                of Absyn.Exp
       | expList            of Absyn.Exp list
       | expListTail        of Absyn.Exp list
       | fldC               of string * Absyn.Exp
       | fldCreat           of (string * Absyn.Exp) list
       | fldCreatTail       of (string * Absyn.Exp) list
       | fldD               of (string * string)
       | fldDec             of (string * string) list
       | fldDecList         of (string * string) list
       | forExp             of Absyn.Exp
       | funDec             of Absyn.FunDec
       | ifThen             of Absyn.Exp
       | ifThenElse         of Absyn.Exp
       | infixExp           of Absyn.Exp
       | letExp             of Absyn.Exp
       | lValue             of Absyn.Exp
       | negation           of Absyn.Exp
       | program            of Absyn.Program
       | recCreate          of Absyn.Exp
       | seqExp             of Absyn.Exp
       | tyDec              of Absyn.TypeDec
       | varDec             of Absyn.VarDec
       | whileExp           of Absyn.Exp


%eop EOF                (* the set of terminals that may follow the start symbol *)
%pure                   (* the semantic actions are free of significant side-effects and always terminate *)
%noshift EOF            (* non-shiftable terminals *)
%arg (fileName) : string

(* Specifying the keywords *)
%keyword ARRAY BREAK DO ELSE END FOR FUNCTION IF IN LET NIL OF THEN TO TYPE VAR WHILE

(* Tokens to insert during error correction *)
%value ID ( "bogus" )
%value TYID ( "bogus" )
%value INTEGER ( 1 )
%value STRING ( "" )

(* replacement suggestions during error correction *)
%change EQUALS -> ASSIGN
      | ASSIGN -> EQUALS
      | SEMICOLON ELSE -> ELSE
      | COMMA -> SEMICOLON
      | SEMICOLON -> COMMA
      |  -> LPAREN 
      |  -> ELSE
      |  -> THEN
      |  -> IN INTEGER END

(* Specifying the precedence and associativity of operators *)
%nonassoc DO OF THEN
%right ELSE
%nonassoc ASSIGN
%left OR
%left AND 
%nonassoc GREATEREQUAL LESSEQUAL EQUALS NOTEQUAL LESS GREATER
%left PLUS MINUS
%left MULTIPLY DIVIDE

%%

(* The grammar associated with our program *)
program     : exp                                                       ( exp )


exp         : NIL                                                       ( Absyn.Nil )
            | INTEGER                                                   ( Absyn.int INTEGER )
            | STRING                                                    ( Absyn.string STRING )
            | lValue                                                    ( lValue )
            | negation                                                  ( negation )
            | seqExp                                                    ( seqExp )
            | callExp                                                   ( callExp )
            | infixExp                                                  ( infixExp )
            | arrCreate                                                 ( arrCreate )
            | recCreate                                                 ( recCreate )
            | assignment                                                ( assignment )
            | ifThenElse                                                ( ifThenElse )
            | ifThen                                                    ( ifThen )
            | whileExp                                                  ( whileExp )
            | forExp                                                    ( forExp )
            | letExp                                                    ( letExp )
            | BREAK                                                     ( Absyn.Break )


seqExp      : LPAREN expList RPAREN                                     ( expList )
expList     :                                                           ( [] )
            | exp expListTail                                           ( exp :: expListTail )
expListTail :                                                           ( [] )
            | SEMICOLON exp expListTail                                 ( exp :: expListTail )


negation    : MINUS exp                                                 ( Absyn.Negation exp )


callExp     : ID LPAREN argList RPAREN                                  ( Absyn.Funcall (ID, argList) )
argList     :                                                           ( [] )
            | exp argListTail                                           ( exp :: argListTail )
argListTail :                                                           ( [] )
            | COMMA exp argListTail                                     ( exp :: argListTail )


infixExp    : exp PLUS exp                                              ( (exp, Absyn.Plus, exp) )
            | exp MINUS exp                                             ( (exp, Absyn.Minus, exp) )
            | exp MULTIPLY exp                                          ( (exp, Absyn.Multiply, exp) )
            | exp DIVIDE exp                                            ( (exp, Absyn.Divide, exp) )
            | exp GREATER exp                                           ( (exp, Absyn.Greater, exp) )
            | exp LESS exp                                              ( (exp, Absyn.Less, exp) )
            | exp EQUALS exp                                            ( (exp, Absyn.Equals, exp) )
            | exp NOTEQUAL exp                                          ( (exp, Absyn.NotEqual, exp) )
            | exp LESSEQUAL exp                                         ( (exp, Absyn.LessEqual, exp) )
            | exp GREATEREQUAL exp                                      ( (exp, Absyn.GreaterEqual, exp) )
            | exp AND exp                                               ( (exp, Absyn.And, exp) )
            | exp OR exp                                                ( (exp, Absyn.Or, exp) )


arrCreate   : TYID LBRACKETS exp RBRACKETS OF exp                       ( Absyn.Array (TYID, exp, exp) )


recCreate   : TYID LBRACES fldCreat RBRACES                             ( Absyn.Record (TYID, fldCreat) )
fldC        : ID EQUALS exp                                             ( (ID, exp) )
fldCreat    :                                                           ( [] )
            | fldC fldCreatTail                                         ( fldC :: fldCreatTail )
fldCreatTail:                                                           ( [] )
            | COMMA fldC fldCreatTail                                   ( fldC :: fldCreatTail )


assignment  : lValue ASSIGN exp                                         ( Absyn.Assignment (lValue, exp) )


ifThenElse  : IF exp THEN exp ELSE exp                                  ( Absyn.IfThenElse (exp, exp, exp) )
ifThen      : IF exp THEN exp                                           ( Absyn.IfThen (exp, exp) )


whileExp    : WHILE exp DO exp                                          ( Absyn.While (exp, exp) )
forExp      : FOR ID ASSIGN exp TO exp DO exp                           ( Absyn.For (ID, exp, exp, exp) )


letExp      : LET decList IN expList END                                ( Absyn.Let (decList, expList) )
decList     : dec                                                       ( [dec] )
            | dec decList                                               ( dec :: decList )


dec         : tyDec                                                     ( Absyn.TyDec tyDec )
            | varDec                                                    ( Absyn.VDec varDec )
            | funDec                                                    ( Absyn.FDec funDec )


tyDec       : TYPE TYID EQUALS TYID                                     ( Absyn.TypeAssignment (TYID, TYID) )
            | TYPE TYID EQUALS ARRAY OF TYID                            ( Absyn.ArrayType (TYID, TYID) )
            | TYPE TYID EQUALS LBRACES fldDec RBRACES                   ( Absyn.RecordType (TYID, fldDec) )
fldD        : ID COLON TYID                                             ( (ID, TYID) )
fldDec      :                                                           ( [] )
            | fldD fldDecList                                           ( fldD :: fldDecList )
fldDecList  :                                                           ( [] )
            | COMMA fldD fldDecList                                     ( fldD :: fldDecList )


funDec      : FUNCTION ID LPAREN fldDec RPAREN EQUALS exp               ( Absyn.Fun (ID, fldDec, exp) )
            | FUNCTION ID LPAREN fldDec RPAREN COLON TYID EQUALS exp    ( Absyn.FunType (ID, fldDec, TYID, exp) )


varDec      : VAR ID ASSIGN exp                                         ( Absyn.Var (ID, exp) )
            | VAR ID COLON TYID ASSIGN exp                              ( Absyn.VarType (ID, TYID, exp) )


lValue      : ID                                                        ( Absyn.Id )
            | lValue LBRACKETS exp RBRACKETS                            ( Absyn.Subscript (lValue, exp) )
            | lValue DOT ID                                             ( Absyn.Field (lValue, ID) )